;本题的难点不在于程序本身，而是对算法的理解
;以下为参考网上资源自我整理的推导过程，要求至少有最基础的二阶矩阵乘法的知识
;「 |
; | 」表示一个矩阵，以下为具体原理：

;「 f(n) | =「0 1| .「f(n-1)| = ... =「0 1|^(n-1) .「f(1)| =「0 1|^(n) .「f(0)| 
; |f(n+1)」  |1 1」  | f(n) 」        |1 1」        |f(2)」  |1 1」      |f(1)」
; 又：
;「0 1|^(n) =「0 1|^(n-1) .「0 1| =「0 1|^(n-1) .「f(0) f(1)| =「f(n-1)  f(n) |
; |1 1」      |1 1」        |1 1」  |1 1」        |f(1) f(2)」  | f(n)  f(n+1)」
; 即：
; 可设变换矩阵 T =「p  q |，初值为 q=1, p=0，则计算 T^n ，即可得出f(n)
;             |q p+q」   

;参照之前类似的迭代乘幂函数，以a和b作为p和q对应的状态变量记录当前幂次的值
;并提前计算好与变换矩阵T与当前幂次的值相乘：（用于奇数）
;「b  a | .「p  q | = 「bp+aq  bq+ap+aq| , => b' <- bp+aq
; |a a+b」  |q p+q」   | ...     ...   」  => a' <- bq+ap+aq
;以及变换矩阵T自身的二次幂：（用于偶数）
;「p  q |^2 = 「p^2+q^2  2pq+q^2|        , => p' <- p^2+q^2
; |q p+q」     |  ...      ...  」         => q' <- 2pq+q^2 


(define (fib n)
    (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q n)
    (cond ((= n 0) b)
          ((even? n) (fib-iter a 
                               b
                               (+ (* p p) (* q q))     ; p'
                               (+ (* 2 p q) (* q q))   ; q'
                               (/ n 2)))
          (else (fib-iter (+ (* b q) (* a q) (* a p))  ; a'
                          (+ (* b p) (* a q))          ; b'
                          p
                          q
                          (- n 1)))))

(display (fib 8));21


(exit)                          